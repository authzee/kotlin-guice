buildscript {
  ext.guice_version = '4.2.0'
  ext.kotlin_version = '1.2.40'

  repositories {
    jcenter()
    mavenCentral()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    classpath 'gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:3.3.0'
    classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
    classpath 'me.champeau.gradle:jmh-gradle-plugin:0.4.5'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'org.jetbrains.dokka:dokka-gradle-plugin:0.9.16'
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'kotlin'
  apply plugin: 'io.spring.dependency-management'
  apply plugin: 'org.jetbrains.dokka'
  apply plugin: 'org.jlleitschuh.gradle.ktlint'
  apply plugin: 'me.champeau.gradle.jmh'
  apply plugin: 'com.github.ben-manes.versions'

  sourceCompatibility = 1.8

  group = GROUP
  version = VERSION_NAME

  repositories {
    jcenter()
    mavenCentral()
  }

  // Use Dokka instead
  tasks.withType(Javadoc) { enabled = false }

  dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"

    externalDocumentationLink {
      url = new URL("http://google.github.io/guice/api-docs/4.2/javadoc/")
    }
  }

  test {
    useJUnitPlatform {
      includeEngines 'spek'
    }
  }

  jmh {
    // For some reason jmh, the annotation processor, or the jmh-gradle-plugin is creating
    // duplicates of the jmh generated classes; one in the build/jmh-generated-classes and
    // one in the build/classes directory. This leads to duplicates being found when copying
    // classes for the jar. Changing this to 'warn' allows us to run the benchmarks even though
    // something isn't configured correctly somewhere.
    duplicateClassesStrategy = 'warn'
  }

  ktlint {
    version = "0.9.2"
  }

  sourceSets {
    jmh
  }

  dependencyManagement {
    dependencies {
      dependency "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
      dependency "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version" // managing version up
      dependency "com.google.inject:guice:$guice_version"

      // Test Dependencies
      dependency 'org.amshove.kluent:kluent:1.36'
      dependency 'org.jetbrains.spek:spek-api:1.1.5'
      dependency 'org.jetbrains.spek:spek-junit-platform-engine:1.1.5'
      dependency 'org.junit.jupiter:junit-jupiter-api:5.1.1'
      dependency 'org.junit.jupiter:junit-jupiter-engine:5.1.1'
    }
  }

  dependencies {
    jmh 'org.openjdk.jmh:jmh-core:1.20'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.20'
  }

  build.dependsOn check
  build.finalizedBy dokka
}

wrapper {
  gradleVersion '4.7'
}
